Build1=Default,cloyd.home.monitor
File1=0.jpg
File2=1.bal
File3=crysta.ttf
File4=off.png
File5=on.png
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
Group=Default Group
IconFile=
Library1=core
Library10=phone
Library2=appcompat
Library3=javaobject
Library4=xmllayoutbuilder
Library5=jmqtt
Library6=randomaccessfile
Library7=byteconverter
Library8=xui
Library9=stringutils
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.basic4ppc.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="7" android:targetSdkVersion="21"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~'End of default text.~\n~~\n~SetApplicationAttribute(android:theme, "@style/MyAppTheme")~\n~~\n~CreateResource(values, theme.xml,~\n~<resources>~\n~    <style name="MyAppTheme" parent="Theme.AppCompat.Light.NoActionBar">~\n~        <item name="colorPrimary">#0098FF</item>~\n~        <item name="colorPrimaryDark">#007CF5</item>~\n~        <item name="colorAccent">#66CD00</item>~\n~        <item name="windowNoTitle">true</item>~\n~        <item name="windowActionBar">false</item>~\n~    </style>~\n~</resources>~\n~)
Module1=CustomListView
Module2=Gauge
NumberOfFiles=5
NumberOfLibraries=10
NumberOfModules=2
Version=7.8
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: Cloyd Home Monitor
	#VersionCode: 1
	#VersionName: 1.0
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: unspecified
	#CanInstallToExternalStorage: False
	#BridgeLogger:True
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: True
#End Region
	#Extends: android.support.v7.app.AppCompatActivity
Sub Process_Globals
	Private MQTT As MqttClient
	Private MQTTUser As String = "vynckfaq"
	Private MQTTPassword As String = "KHSV1Q1qSUUY"
	Private MQTTServerURI As String = "tcp://m14.cloudmqtt.com:11816"
	Private cartBitmap As Bitmap
	Private bc As ByteConverter
	Private badge As Int

End Sub

Sub Globals
	Private ACToolBarLight1 As ACToolBarLight
	Private ToolbarHelper As ACActionBar
	Private clv1 As CustomListView
	Private gblACMenu As ACMenu
	Private xui As XUI
	Private GaugeHumidity As Gauge
	Private GaugeTemp As Gauge
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Try
		If FirstTime Then
	        'CallSub(Me, MQTT_Connect)
			MQTT_Connect
	    End If
		Activity.LoadLayout("1")
		Dim bd As BitmapDrawable
		bd.Initialize(LoadBitmapResize(File.DirAssets, "0.jpg", 32dip, 32dip, True))
		ACToolBarLight1.NavigationIconDrawable = bd
		ToolbarHelper.Initialize
		Dim cs As CSBuilder
		ToolbarHelper.Title= cs.Initialize.Size(18).Append("Cloyd Home Monitor v").PopAll & GetVersionCode
		ToolbarHelper.subTitle = ""
		ToolbarHelper.ShowUpIndicator = False 'set to true to show the up arrow
		ACToolBarLight1.InitMenuListener
		Dim jo As JavaObject = ACToolBarLight1
		Dim xl As XmlLayoutBuilder
		jo.RunMethod("setPopupTheme", Array(xl.GetResourceId("style", "ToolbarMenu")))	
		
		GaugeHumidity.SetRanges(Array(GaugeHumidity.CreateRange(0, 68, xui.Color_Green), _
		GaugeHumidity.CreateRange(68, 80, xui.Color_Yellow), _
		GaugeHumidity.CreateRange(80, 100, xui.Color_Red)))
		GaugeTemp.SetRanges(Array(GaugeTemp.CreateRange(0, 78, xui.Color_Green), _
	    GaugeTemp.CreateRange(78, 90, xui.Color_Yellow), _
	    GaugeTemp.CreateRange(90, 100, xui.Color_Red)))	
	Catch
		ToastMessageShow(LastException,True)
	End Try
End Sub

Sub Activity_Resume
	Try
		If MQTT.IsInitialized = False Or MQTT.Connected = False Then
			MQTT_Connect
		End If
	Catch
		Log(LastException)
	End Try
End Sub

Sub Activity_Pause (UserClosed As Boolean)

End Sub

'Connect to CloudMQTT broker
Sub MQTT_Connect
	Try
		Dim ClientId As String = Rnd(0, 999999999) 'create a unique id
        MQTT.Initialize("MQTT", MQTTServerURI, ClientId)

        Dim ConnOpt As MqttConnectOptions
        ConnOpt.Initialize(MQTTUser, MQTTPassword)
        MQTT.Connect2(ConnOpt)
	Catch
		AddEvent("MQTT_Connect: " & LastException)
	End Try
End Sub

Sub MQTT_Connected (Success As Boolean)
	Try	
		If Success = False Then
	        Log(LastException)
			AddEvent("MQTT error connecting")
	    Else
			AddEvent("Connected to MQTT broker")
	        MQTT.Subscribe("Cloyd", 0)
	    End If
	Catch
		AddEvent("MQTT_Connected: " & LastException)
	End Try
    
End Sub

Private Sub MQTT_Disconnected
	Try
		gblACMenu.Clear
		gblACMenu.Add(0, 0, "Restart board",Null)
		gblACMenu.Add(0, 0, "About",Null)
		AddEvent("Disconnected from MQTT broker")
	Catch
		Log(LastException)
	End Try
End Sub

Private Sub MQTT_MessageArrived (Topic As String, Payload() As Byte)
	Try
		Dim status As String 
		status = BytesToString(Payload, 0, Payload.Length, "UTF8")
		If status="*Relay has been opened" Or status = "*Relay is open" Then
			ShowRelayIcon("on.png")
		else if status = "*Relay has been closed" Or status = "*Relay is closed" Then
			ShowRelayIcon("off.png")
		End If
		AddEvent(status)
		Dim a As Int = status.IndexOf("Humidity =")
		If a <> -1 Then
			Dim b As String = status.SubString2(0,status.IndexOf("%"))
			b = b.Replace("Humidity =","")
			Log(b)
			GaugeHumidity.CurrentValue = b
		Else
			GaugeHumidity.CurrentValue = 0
		End If
		Dim a As Int = status.IndexOf("Temperature =")
		If a <> -1 Then
			Dim b As String = status.SubString2(status.IndexOf("Temperature ="),status.IndexOf("º"))
			b = b.Replace("Temperature =","")
			Log(b)
			GaugeTemp.CurrentValue = b
		Else
			GaugeTemp.CurrentValue = 0
		End If
	Catch
		AddEvent("MQTT_MessageArrived: " & LastException)
	End Try
End Sub

Sub AddEvent(text As String)
	Try
		DateTime.DateFormat = "MMM d h:mm:ss a"
		clv1.AddTextItem(DateTime.Date(DateTime.Now) & ": " & text, text)
		Sleep(100)
		clv1.ScrollToItem(clv1.Size-1)
	Catch
		Log(LastException)
	End Try
End Sub

Sub ACToolBarLight1_MenuItemClick (Item As ACMenuItem)
	Try
		If Item.Title = "About" Then
			ShowAboutMenu
		else if Item.Title = "Restart board" Then
			Try
				Dim result As Int
				Dim bd As BitmapDrawable
				bd.Initialize(LoadBitmapResize(File.DirAssets, "0.jpg", 32dip, 32dip, True))
				result = Msgbox2("Restart the controller?", "Cloyd Home Monitor", "Yes", "", "No", bd.Bitmap)
				If result = DialogResponse.POSITIVE Then
					If MQTT.IsInitialized = False Or MQTT.Connected = False Then
						MQTT_Connect
					End If
					MQTT.Publish("Cloyd", bc.StringToBytes("Restart controller", "utf8"))
				End If
			Catch
				AddEvent(LastException)
			End Try
		End If
	Catch
		Log(LastException)
	End Try
End Sub

Sub ShowAboutMenu
	Try
		Dim bd As BitmapDrawable
		bd.Initialize(LoadBitmapResize(File.DirAssets, "0.jpg", 32dip, 32dip, True))
		Msgbox2("Cloyd Home Monitor v" & GetVersionCode & CRLF & "Developed by Cloyd Catanaoan" & CRLF & "March 13, 2018", "About", "OK", "", "", bd.Bitmap)
	Catch
		Log(LastException)
	End Try
	
End Sub

Sub GetVersionCode() As String
	Dim AppVersion As String
	Try
		Dim pm As PackageManager
	    Dim packageName As String
	    packageName =  Application.PackageName
	    AppVersion = pm.GetVersionName(packageName)
	Catch
		Log(LastException)
	End Try
	Return AppVersion
End Sub

Sub Activity_Createmenu(Menu As ACMenu)
	Try
		Menu.Clear
		gblACMenu = Menu
		Menu.Add(0, 0, "Restart board",Null)
		Menu.Add(0, 0, "About",Null)
	Catch
		Log(LastException)
	End Try
End Sub

Sub ShowRelayIcon(state As String)
	Try
		gblACMenu.Clear
		Dim item As ACMenuItem = ACToolBarLight1.Menu.Add2(0, 0, "relay", Null)
		item.ShowAsAction = item.SHOW_AS_ACTION_ALWAYS
		cartBitmap = LoadBitmap(File.DirAssets, state)
		UpdateIcon("relay", AddBadgeToIcon(cartBitmap, badge))
		gblACMenu.Add(0, 0, "Restart board",Null)
		gblACMenu.Add(0, 0, "About",Null)
	Catch
		Log(LastException)
	End Try
End Sub

Sub UpdateIcon(MenuTitle As String, Icon As Bitmap)
	Try
		Dim m As ACMenuItem = GetMenuItem(MenuTitle)
		m.Icon = BitmapToBitmapDrawable(Icon)
	Catch
		Log(LastException)
	End Try
End Sub

Sub BitmapToBitmapDrawable (bitmap As Bitmap) As BitmapDrawable
	Try
		Dim bd As BitmapDrawable
		bd.Initialize(bitmap)
		Return bd
	Catch
		Log(LastException)
		Return Null
	End Try
End Sub

Sub GetMenuItem(Title As String) As ACMenuItem
	Try
		For i = 0 To ACToolBarLight1.Menu.Size - 1
			Dim m As ACMenuItem = ACToolBarLight1.Menu.GetItem(i)
			If m.Title = Title Then
				Return m
			End If
		Next
		Return Null
	Catch
		Log(LastException)
		Return Null
	End Try
	
End Sub

Sub AddBadgeToIcon(bmp As Bitmap, Number As Int) As Bitmap
	Dim mbmp As Bitmap
	Try
		Dim cvs As Canvas
		mbmp.InitializeMutable(32dip, 32dip)
		cvs.Initialize2(mbmp)
		Dim target As Rect
		target.Initialize(0, 0, mbmp.Width, mbmp.Height)
		cvs.DrawBitmap(bmp, Null, target)
		If Number > 0 Then
			cvs.DrawCircle(mbmp.Width - 8dip, 8dip, 8dip, Colors.Red, True, 0)
			cvs.DrawText(Min(Number, 9), mbmp.Width - 8dip, 12dip, Typeface.DEFAULT_BOLD, 12, Colors.White, "CENTER")
		End If
		Return mbmp
	Catch
		Log(LastException)
		Return Null
	End Try
End Sub

#If Java

public boolean _onCreateOptionsMenu(android.view.Menu menu) {
    if (processBA.subExists("activity_createmenu")) {
        processBA.raiseEvent2(null, true, "activity_createmenu", false, new de.amberhome.objects.appcompat.ACMenuWrapper(menu));
        return true;
    }
    else
        return false;
}
#End If